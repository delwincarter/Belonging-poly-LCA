TITLE:
Step 3 - 3step Belonging LCA
DATA:
FILE = "/Users/brucecarter/Library/CloudStorage/Box-Box/Belonging-S&D/3step/stress_s3.dat";
 
VARIABLE:
NAMES = WHITE GPA FIRSTGEN ESTEEM STRESS LATINX ASIAN N; 
 MISSING=.;
 nominal = N;
     classes = c(4);
ANALYSIS:
estimator = mlr;
     type = mixture;
     starts = 0;
     processors = 10;
MODEL:
%OVERALL%
C on FIRSTGEN WHITE LATINX ASIAN;
GPA ESTEEM STRESS on FIRSTGEN WHITE LATINX ASIAN;
[GPA];
GPA;
[STRESS];
STRESS;
[ESTEEM];
ESTEEM;

%C#1%
[N#1@9.699];
[N#2@4.809];
[N#3@7.068];
[GPA] (GPA1);
GPA;
[STRESS] (STRESS1);
STRESS;
[ESTEEM] (ESTEEM1);
ESTEEM;

%C#2%
[N#1@-0.348];
[N#2@3.008];
[N#3@0.509];
[GPA] (GPA2);
GPA;
[STRESS] (STRESS2);
STRESS;
[ESTEEM] (ESTEEM2);
ESTEEM;

%C#3%
[N#1@1.493];
[N#2@0.807];
[N#3@4.279];
[GPA] (GPA3);
GPA;
[STRESS] (STRESS3);
STRESS;
[ESTEEM] (ESTEEM3);
ESTEEM;

%C#4%
[N#1@-11.152];
[N#2@-2.775];
[N#3@-2.501];
[GPA] (GPA4);
GPA;
[STRESS] (STRESS4);
STRESS;
[ESTEEM] (ESTEEM4);
ESTEEM;
MODEL CONSTRAINT:
NEW(GPA12 GPA13 GPA14 GPA23 GPA24 GPA34 STRESS12 STRESS13 STRESS14
        STRESS23 STRESS24 STRESS34 ESTEEM12 ESTEEM13 ESTEEM14
        ESTEEM23 ESTEEM24 ESTEEM34);
     GPA12 = GPA1-GPA2;
     GPA13 = GPA1-GPA3;
     GPA14 = GPA1-GPA4;
     GPA23 = GPA2-GPA3;
     GPA24 = GPA2-GPA4;
     GPA34 = GPA3-GPA4;
     STRESS12 = STRESS1-STRESS2;
     STRESS13 = STRESS1-STRESS3;
     STRESS14 = STRESS1-STRESS4;
     STRESS23 = STRESS2-STRESS3;
     STRESS24 = STRESS2-STRESS4;
     STRESS34 = STRESS3-STRESS4;
     ESTEEM12 = ESTEEM1-ESTEEM2;
     ESTEEM13 = ESTEEM1-ESTEEM3;
     ESTEEM14 = ESTEEM1-ESTEEM4;
     ESTEEM23 = ESTEEM2-ESTEEM3;
     ESTEEM24 = ESTEEM2-ESTEEM4;
     ESTEEM34 = ESTEEM3-ESTEEM4;
MODEL TEST:
!0 = GPA1-GPA2;
     !0 = GPA1-GPA3;
     !0 = GPA1-GPA4;
     0 = STRESS1-STRESS2;
     0 = STRESS2-STRESS3;
     0 = STRESS3-STRESS4;
     !0 = ESTEEM1-ESTEEM2;
     !0 = ESTEEM1-ESTEEM3;
     !0 = ESTEEM1-ESTEEM4;
OUTPUT:
tech1 tech8 standardized;
